/*
 *  GNU GENERAL PUBLIC LICENSE Version 3, 29 June 2007
 *
 *  Copyright (C) 2021 Free Software Foundation, Inc. <https://fsf.org/> Everyone is permitted to
 *  copy and distribute verbatim copies of this license document, but changing it is not allowed.
 *
 *  Yusuf Arfan Ismail
 *
 *  The GNU General Public License is a free, copyleft license for software and other kinds of works.
 */

plugins {
    id 'java'
    id "com.diffplug.spotless" version "5.16.0"
    id "name.remal.sonarlint" version "1.5.0"
    id "org.sonarqube" version "3.3"
}

version 'v3-1.0.0'
group 'io.github.yusufsdiscordbot'

sourceCompatibility = JavaVersion.VERSION_17

sonarqube {
    properties {
        property "sonar.projectKey", "YusufsDiscordbot_Yusuf-s-Moderation-Bot"
        property "sonar.organization", "yusufsdiscordbot"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
subprojects {
    //credits for some of this part of the gradle goes toward https://github.com/Together-Java/TJ-Bot/blob/a2a963a9e51792018f1beff3b41b6ba9eab0ca50/build.gradle#L25
    apply plugin: "java"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "org.sonarqube"

    // All subprojects inherit root project group and version, to avoid duplication.
    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    compileJava {
        // Makes spotlessApply task run on every compile/build.
        dependsOn 'spotlessApply'

        // Nails the Java-Version of every Subproject
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    spotless {
        java {
            // Excludes build folder since it contains generated java classes.
            targetExclude("build/**")
            eclipse('4.19.0').configFile("${rootProject.rootDir}/meta/formatting/google-style-eclipse.xml")
        }
    }
}
